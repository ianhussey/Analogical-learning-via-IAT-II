**************************************************************************************************************
**************************************************************************************************************
									POLICE OFFICER DILEMMA TASK (PODT)
*************************************************************************************************************
**************************************************************************************************************
														SCRIPT INFO

Script Author: Katja Borchert, Ph.D. (katjab@millisecond.com) for Millisecond Software LLC
Date: 04-12-2012

last updated: 08-14-2012
**************************************************************************************************************
**************************************************************************************************************
									SCRIPT DESCRIPTION/BACKGROUND INFO
**************************************************************************************************************
**************************************************************************************************************
This script provides the Police Officer Dilemma Task (PODT)  as described in (Experiment1):

Correll, J., Park, B., Judd, C.M., & Wittenbrink, B. (2002). The Police Officer's Dilemma:
Using Ethnicity to Disambiguate Potentially Threating Individuals. Journal of Personality 
and Social Psychology, 83, 1314-1329.

All images used in this script are the originals. Millisecond Software thanks Dr. Correll et al for
providing generous assistance!

														Script Specifics:
(1) Design: 2 (ethnicity: black vs. white) x 2 (gun vs. no gun) = 4 experimental conditions, 
all tested within participants (see Correll et al, 2002)
(2) Counterbalanced for shootkey vs. no shootkey (Default: A and L keys, change
possible under values)
(3) 1 Experimental Block that runs 80 trials:  20 trials in each of the 4 conditions,
 conditions are randomly sampled without replacement (see list.condition for details)
There are 20 slides for each condition, each slide is sampled once (see Correll et al, 2002)
(see list.itemnumber1 and others for details)
(4) Trials are set up as trial-combos:
Fixation -> Background (variable number) -> Experimental Condition -> Feedback
(5) Number of background images in background slideshow: randomly 
(with replacement) determined, between 1-4 (see list.repetition) (-> Correll et al, 2002)
(6)  Duration of backgrounds btw 500-1000ms (increments of 100ms),
 randomly determined with replacement (see list.duration)
(7) Duration of Fixation cross in this script: 500ms (change under values)
(8) Duration of Feedback: 3000ms (change under values)
(9) Rewards/Punishments: see  Correll et al. (2002), Experiment 1
(changes possible under values)
(10) Response Timeout: default 850ms see  Correll et al. (2002), (change under values)
(11) Instructions easily adaptable under Instructions. Current Instructions modelled after
the latest instructions used by Correll et al (these later instructions are 
less detailed than the original 2002 instructions)
(12) Script offers a practice block of 20 trial combos. Practice material, most trials, and blocks are
separately coded from Experimental ones. 
**************************************************************************************************************
**************************************************************************************************************
												DEFAULT SCREEN SETTING
**************************************************************************************************************
**************************************************************************************************************
requires Inquisit 4.0.0.0

<defaults >
/ minimumversion = "4.0.0.0"
</defaults>

**************************************************************************************************************
**************************************************************************************************************
															VALUES
**************************************************************************************************************
**************************************************************************************************************

***************************
editable (under values, see below)
***************************
HitReward....FAPunish: 					amounts earned or lost depending on Hits, Misses, 
													Correct Rejections (CR), False Alarms (FA)
fixationduration:							the duration of the fixation cross (default: 500, original)
key1/key2:									the scancodes of the shoot and noshoot keys
													(keys are counterbalanced), default: "A" and "L"
timeout:										timeout of main experimental trials, default = 850ms
feedbackduration:							the duration of the feedback, default = 3000

changes can be made below under VALUES

<values>
/HitReward = 10
/MissPunish = 40
/CRReward = 5
/FAPunish = 20
/NoresponsePunish = 10
/fixationduration = 500
/key1 = 30
/key2 = 38
/timeout =850
/feedbackduration = 3000
</values>

*****************************
automatically updated at runtime
*****************************
group:												counterbalanced shootkey groups (1/2)
shootkey/noshootkey:							counterbalancing of key1 and key2
															(final values assigned during runtime)
selectbackground:									determines which background picture is selected
condition:												1 = GunWhite; 2 = GunBlack; 3 = ObjectWhite; 4 = ObjectBlack
itemnumber:											determines the itemnumber of the target image
repetition:												determines how many background images areto be  presented
Hit....CR:												codes whether the response was a HIT, a MISS, a FA, or a CR
result:													either "Hit", "Miss", "CR", "FA" or "Noresponse"
total:														keeps a running tab of the total money earned (or lost)
backgroundduration:								determines how long each background slide is presented
count_background:								counts how many background slides have been presented
selectfeedback:										helper variable to display correct feedback
SumWhiteHits....SumBlackCRs:				keeps a running total of Hits, Misses, FAs, CRs for each ethnicity
SumRTWhiteHits...SumRTBlackCRs:			keeps a running total of the reaction times for Hits, Misses, FAs, CRs for each ethnicity
countGunWhite...countObjectBlack:			counts the trials in each condition

<values>
/group = 0
/shootkey = values.key1
/noshootkey = values.key2
/selectbackground = 1
/condition = 0
/itemnumber = 0
/repetition = 0
/Hit = 0
/Miss = 0
/FA = 0
/CR = 0
/result = ""
/total = 0
/backgroundduration = 0
/count_background = 0
/selectfeedback = 0

/Sum_WhiteHits = 0
/Sum_WhiteMisses = 0
/Sum_WhiteFAs = 0
/Sum_WhiteCRs = 0
/Sum_BlackHits = 0
/Sum_BlackMisses = 0
/Sum_BlackFAs = 0
/Sum_BlackCRs = 0

/SumRT_WhiteHits = 0
/SumRT_WhiteMisses = 0
/SumRT_WhiteFAs = 0
/SumRT_WhiteCRs = 0
/SumRT_BlackHits = 0
/SumRT_BlackMisses = 0
/SumRT_BlackFAs = 0
/SumRT_BlackCRs = 0

/count_GunWhite = 0
/count_GunBlack = 0
/count_ObjectBlack = 0
/count_ObjectWhite = 0
</values>
**************************************************************************************************************
**************************************************************************************************************
													EXPRESSIONS
**************************************************************************************************************
**************************************************************************************************************
MeanRT_WhiteHits...MeanRT_BlackCRs:				calculates the mean reaction times
																			for Hits, Misses, FAs, CRs for each ethnicity

%_WhiteHits...%_BlackCRs:									calculates the percentages of Hits-Misses and
																			CRs-FAs for each ethnicity


<expressions >
/MeanRT_WhiteHits = values.SumRT_WhiteHits / values.Sum_WhiteHits
/MeanRT_WhiteMisses = values.SumRT_WhiteMisses / values.Sum_WhiteMisses
/MeanRT_WhiteFAs = values.SumRT_WhiteFAs / values.Sum_WhiteFAs
/MeanRT_WhiteCRs = values.SumRT_WhiteCRs / values.Sum_WhiteCRs
/MeanRT_BlackHits = values.SumRT_BlackHits / values.Sum_BlackHits
/MeanRT_BlackMisses = values.SumRT_BlackMisses / values.Sum_BlackMisses
/MeanRT_BlackFAs = values.SumRT_BlackFAs / values.Sum_BlackFAs
/MeanRT_BlackCRs = values.SumRT_BlackCRs / values.Sum_BlackCRs

/%_WhiteHits = 100* (values.Sum_WhiteHits /values.count_GunWhite)
/%_WhiteMisses = 100 * (values.Sum_WhiteMisses /values.count_GunWhite)
/%_WhiteFAs = 100 * (values.Sum_WhiteFAs /values.count_ObjectWhite)
/%_WhiteCRs = 100 * (values.Sum_WhiteCRs /values.count_ObjectWhite)
/%_BlackHits = 100 * (values.Sum_BlackHits /values.count_GunBlack)
/%_BlackMisses = 100 * (values.Sum_BlackMisses /values.count_GunBlack)
/%_BlackFAs = 100 * (values.Sum_BlackFAs /values.count_ObjectBlack)
/%_BlackCRs = 100 * (values.Sum_BlackCRs /values.count_ObjectBlack)
</expressions>

**************************************************************************************************************
**************************************************************************************************************
														DATA
**************************************************************************************************************
**************************************************************************************************************

******************
raw data
******************
<data >
/file = "PODT_rawdata.iqdat"
/ separatefiles = true
/ columns = [date, time, subject, values.group, trialcode, values.itemnumber, values.selectbackground,
values.repetition, values.backgroundduration, values.count_background,
response, correct, values.result, latency, values.total,
values.count_GunWhite, values.count_ObjectWhite, values.count_GunBlack, values.count_ObjectBlack,
values.SumRT_WhiteHits; values.Sum_WhiteHits; expressions.MeanRT_WhiteHits, expressions.%_WhiteHits,
values.SumRT_WhiteMisses; values.Sum_WhiteMisses; expressions.MeanRT_WhiteMisses, expressions.%_WhiteMisses,
values.SumRT_WhiteFAs; values.Sum_WhiteFAs; expressions.MeanRT_WhiteFAs, expressions.%_WhiteFAs,
values.SumRT_WhiteCRs; values.Sum_WhiteCRs; expressions.MeanRT_WhiteCRs, expressions.%_WhiteCRs,
values.SumRT_BlackHits; values.Sum_BlackHits; expressions.MeanRT_BlackHits, expressions.%_BlackHits,
values.SumRT_BlackMisses; values.Sum_BlackMisses; expressions.MeanRT_BlackMisses, expressions.%_BlackMisses,
values.SumRT_BlackFAs; values.Sum_BlackFAs; expressions.MeanRT_BlackFAs, expressions.%_BlackFAs,
values.SumRT_BlackCRs; values.Sum_BlackCRs; expressions.MeanRT_BlackCRs, expressions.%_BlackCRs]
</data>

******************
summary data
******************
<summarydata >
/file = "PODT_summarydata.iqdat"
/columns = [script.startdate, script.starttime, script.subjectid, values.group, values.total, 
values.count_GunWhite, values.count_ObjectWhite, values.count_GunBlack, values.count_ObjectBlack,
values.SumRT_WhiteHits; values.Sum_WhiteHits; expressions.MeanRT_WhiteHits, expressions.%_WhiteHits,
values.SumRT_WhiteMisses; values.Sum_WhiteMisses; expressions.MeanRT_WhiteMisses, expressions.%_WhiteMisses,
values.SumRT_WhiteFAs; values.Sum_WhiteFAs; expressions.MeanRT_WhiteFAs, expressions.%_WhiteFAs,
values.SumRT_WhiteCRs; values.Sum_WhiteCRs; expressions.MeanRT_WhiteCRs, expressions.%_WhiteCRs,
values.SumRT_BlackHits; values.Sum_BlackHits; expressions.MeanRT_BlackHits, expressions.%_BlackHits,
values.SumRT_BlackMisses; values.Sum_BlackMisses; expressions.MeanRT_BlackMisses, expressions.%_BlackMisses,
values.SumRT_BlackFAs; values.Sum_BlackFAs; expressions.MeanRT_BlackFAs, expressions.%_BlackFAs,
values.SumRT_BlackCRs; values.Sum_BlackCRs; expressions.MeanRT_BlackCRs, expressions.%_BlackCRs]
</summarydata>



**************************************************************************************************************
**************************************************************************************************************
													INSTRUCTIONS
**************************************************************************************************************
**************************************************************************************************************
<instruct >
/ fontstyle = ("Arial", 2.5%, false, false, false, false, 5, 0)
/ lastlabel = "<LEERTASTE> um fortzufahren"
/ nextlabel = "<LEERTASTE> um fortzufahren"
/ nextkey = (57)
/ prevlabel = "<ENTER> um zurückzugehen"
/ prevkey = (28)
/ windowsize = (90%, 80%)
</instruct>

***********************************************
General Intro
***********************************************
Note: 
Current Instructions modelled after
the latest instructions used by Correll et al (these later  instructions are 
less detailed than the original 2002 instructions)
^^In this videogame, your task is to shoot any person who is holding
a gun (the bad guys) by pressing the "A" button on your keyboard (A = shoot button). 
^^If a person is holding something other than a gun he is a good guy, and
you should press the the "L" button on your keyboard (L = NO shoot button).
^^
^^You will have less than a second to make each decision.
^
^^You will receive points based on your performance.
^
^^The first round of the game is for practice.
^
^^If you have any questions, call the experimenter now.
^
^^If you are ready to begin the practice round, press the SPACE BAR.

<page Intro>
^^^^Vielen Dank für Ihre Teilnahme an dieser Untersuchung!
^^^Auf der nächsten Seite wird Ihnen erläutert, worum es sich dabei im Detail handelt.
</page>

<page intro1_A>
^^Im folgenden Videospiel ist es Ihre Aufgabe auf jede Person zu schießen die eine Waffe in der Hand hält ("die Bösen"),
^indem sie die linke Antworttaste drücken (links = schießen). 
^^Wenn eine Person einen anderen Gegenstand in der Hand hält, gehört sie zu den "Guten" und sie müssen die 
^rechte Antworttaste drücken (rechts = NICHT schießen).
^^
^^Sie haben jeweils weniger als eine Sekunde Zeit um zu entscheiden.
^
^^Basierend auf Ihrer Leistung erhalten Sie pro Durchgang Punkte.
^
^^Die erste Runde des Spiels dient der Übung.
^
^^Falls Sie noch Fragen haben, wenden Sich bitte jetzt an den Versuchsleiter/ die Versuchsleiterin.
^
^^Wenn Sie bereit sind mit dem Übungsdurchgang zu beginnen, dann drücken sie nun die LEERTASTE.
</page>


<page intro1_B>
^^Im folgenden Videospiel ist es Ihre Aufgabe auf jede Person zu schießen die eine Waffe in der Hand hält ("die Bösen"),
^indem sie die rechte Antworttaste drücken (rechts = schießen). 
^^Wenn eine Person einen anderen Gegenstand in der Hand hält, gehört sie zu den "Guten" und sie müssen die 
^linke Antworttaste drücken (links = NICHT schießen).
^^
^^Sie haben jeweils weniger als eine Sekunde Zeit um zu entscheiden.
^
^^Basierend auf Ihrer Leistung erhalten Sie pro Durchgang Punkte.
^
^^Die erste Runde des Spiels dient der Übung.
^
^^Falls Sie noch Fragen haben, wenden Sich bitte jetzt an den Versuchsleiter/ die Versuchsleiterin.
^
^^Wenn Sie bereit sind mit dem Übungsdurchgang zu beginnen, dann drücken sie nun die LEERTASTE.
</page>


***********************************************
Practice Block Instructions
***********************************************
                             This is a PRACTICE SESSION. 
^^^^Reminder:
^^                                                        YOUR TASK IS:
^^To decide as quickly and accurately as possible to shoot or not to shoot. 
^^(1) If the person holds a GUN you need to SHOOT: 
^                                                           press "L" on the keyboard. (L = shoot button) 
^^(2) If the person holds a HARMLESS OBJECT, do NOT shoot:  
^                                                           press "A" on your keyboard (A = No shoot button)
^^
^^IMPORTANT: use fingers of different hands for each response key.


<page practiceA>
                           Dies ist ein ÜBUNGSDURCHGANG.
^^^^Zur Erinnerung:
^^                                                         Ihre Aufgabe:
^^Entscheiden Sie so schnell und akkurat wie möglich zu schießen oder nicht zu schießen. 
^^(1) Wenn die Person eine WAFFE in der Hand hält müssen sie SCHIEßEN: 
^                                                           Drücken Sie die linke Antworttaste (links = schießen) 
^^(2) Wenn die Person einen HARMLOSEN GEGENSTAND in der Hand hält sollen sie NICHT schießen:  
^                                                           Drücken Sie die rechte Antworttaste (rechts = nicht schießen)
^^
^^Wichtig: Benutzen Sie bitte die Finger beider Hände für die Aufgabe.
</page>

<page practiceB>
                           Dies ist ein ÜBUNGSDURCHGANG.
^^^^Zur Erinnerung:
^^                                                         Ihre Aufgabe:
^^Entscheiden Sie so schnell und akkurat wie möglich zu schießen oder nicht zu schießen. 
^^(1) Wenn die Person eine WAFFE in der Hand hält müssen sie SCHIEßEN: 
^                                                           Drücken Sie die rechte Antworttaste (rechts = schießen) 
^^(2) Wenn die Person einen HARMLOSEN GEGENSTAND in der Hand hält sollen sie NICHT schießen:  
^                                                           Drücken Sie die linke Antworttaste (links = nicht schießen)
^^
^^Wichtig: Benutzen Sie bitte die Finger beider Hände für die Aufgabe.
</page>

***********************************************
Experimental Block Instructions
***********************************************
You are done with practice. If you have anymore questions, please ask them now.
^^^^Reminder:
^^                                                        YOUR TASK IS:
^^To decide as quickly and accurately as possible to shoot or not to shoot. 
^^(1) If the person holds a GUN you need to SHOOT: 
^                                                           press "A" on the keyboard. (A = shoot button) 
^^(2) If the person holds a HARMLESS OBJECT, do NOT shoot:  
^                                                           press "L" on your keyboard (L = No shoot button)
^^
^^IMPORTANT: use fingers of different hands for each response key.
^^If you are ready, start now.


<page taskA>
Der Übungsdurchgang ist nun beendet. Wenn Sie noch Fragen haben, stellen Sie sie bitte jetzt.
^^^^Zur Erinnerung:
^^                                                         Ihre Aufgabe:
^^Entscheiden Sie so schnell und akkurat wie möglich zu schießen oder nicht zu schießen. 
^^(1) Wenn die Person eine WAFFE in der Hand hält müssen sie SCHIEßEN: 
^                                                           Drücken Sie die linke Antworttaste (links = schießen) 
^^(2) Wenn die Person einen HARMLOSEN GEGENSTAND in der Hand hält sollen sie NICHT schießen:  
^                                                           Drücken Sie die Taste rechte Antworttaste (rechts = nicht schießen)
^^
^^Wichtig: Benutzen Sie bitte die Finger beider Hände für die Aufgabe.
^^Wenn Sie bereit sind, dann fangen sie jetzt an.
</page>

<page taskB>
Der Übungsdurchgang ist nun beendet. Wenn Sie noch Fragen haben, stellen Sie sie bitte jetzt.
^^^^Zur Erinnerung:
^^                                                         Ihre Aufgabe:
^^Entscheiden Sie so schnell und akkurat wie möglich zu schießen oder nicht zu schießen. 
^^(1) Wenn die Person eine WAFFE in der Hand hält müssen sie SCHIEßEN: 
^                                                           Drücken Sie die rechte Antworttaste (rechts = schießen) 
^^(2) Wenn die Person einen HARMLOSEN GEGENSTAND in der Hand hält sollen sie NICHT schießen:  
^                                                           Drücken Sie die linke Antworttaste (links = nicht schießen)
^^
^^Wichtig: Benutzen Sie bitte die Finger beider Hände für die Aufgabe.
^^Wenn Sie bereit sind, dann fangen sie jetzt an.
</page>

<page end>
Der erste Aufgabenblock ist nun beendet!
</page>

<page results>
^^Ihre individuellen Ergebnisse:
^Akkuratheit:
^~tBei bewaffneten Personen: <%(expressions.%_WhiteHits+expressions.%_BlackHits)/2%>%.
^~tBei unbewaffneten Personen: <%(expressions.%_WhiteCRs+expressions.%_BlackCRs)/2%>%.^^^
^Reaktionsgeschwindigkeit (der korrekten Reaktionen):^
^~tBei bewaffneten Personen: <%(expressions.MeanRT_WhiteHits+expressions.MeanRT_BlackHits)/2%>ms.
^~tBei unbewaffneten Personen: <%(expressions.MeanRT_WhiteCRs+expressions.MeanRT_BlackCRs)/2%>ms.
</page>

****************ALTERNATIVE INSTRUCTION TEXTS: more detailed******************************************
Alternative Instructions: not original

^^PERCEPTUAL VIGILANCE STUDY - Ability to quickly respond to a variety of stimuli

^^In this study, you will be shown a variety of slides depicting a wide range of scenes, such as a park
or a store front. At some unpredictable time, a man will appear in one of the scenes holding an object. 
This object can either be a gun or a harmless object such as a cellphone or a soda can.

^^^^                                                         YOUR TASK IS:
^^To decide as quickly and accurately as possible to shoot or not to shoot. 
^^(1) If the man holds a GUN you need to SHOOT: 
^                                                          press "A" on the keyboard. (A = shoot button) 
^^(2) If the man holds a HARMLESS OBJECT, do NOT shoot:  
^                                                           press "L" on your keyboard (L = No shoot button)
^^



Note: 
*original experiment used detailed instructions including a summary of the reward matrix.
In later studies this information was dropped from the instructions
*these are not the original instructions but include the reward matrix info

<page intro2>
Wir zeigen Ihnen einen Zwischenpunktestand, damit Sie sehen, wie gut Sie in der Aufgabe abschneiden.
^^***************************************************************************************************
^Die Entscheidungen, die Sie treffen, führen dazu, dass Sie entweder Punkte gewinnen oder Punkte verlieren:
^^Sie SCHIEßEN, wenn die Person eine Waffe hält:                                 Sie erhalten <%values.HitReward%>  Punkte (+<%values.HitReward%>)
^^Sie schießen NICHT, wenn die Person eine Waffe hält:                      Sie verlieren <%values.MissPunish%>  points (-<%values.MissPunish%>)
^^Sie schießen NICHT, wenn die Person UNBEWAFFNET ist:                     Sie gewinnen <%values.CRReward%>  points (+<%values.CRReward%>) 
^^Sie schießen, wenn die Person UNBEWAFFNET ist:                                Sie verlieren <%values.FAPunish%> points (-<%values.FAPunish%>)
^^Wenn Sie nicht schnell genug reagieren, werden Ihnen <%values.NoresponsePunish%>  Punkte abgezogen (-<%values.NoresponsePunish%>)

^^WICHTIG: Sie bekommen unmittelbar nach jeder Entscheidung ein Feedback und Ihren aktuellen Punktestand mitgeteilt.
^**************************************************************************************************


^^Damit Sie sich mit der Aufgabe vertraut machen können, beginnen Sie bitte mit dem Übungsblock.
</page>
 

**************************************************************************************************************
**************************************************************************************************************
													  PRACTICE STIMULI
**************************************************************************************************************
**************************************************************************************************************
<item backgrounds_practice>
/1 = "wu01.jpg"
/2 = "wu02.jpg"
/3 = "wu03.jpg"
/4 = "wu04.jpg"
/5 = "wu05.jpg"
/6 = "wu06.jpg"
/7 = "wu07.jpg"
/8 = "wu08.jpg"
/9 = "wu09.jpg"
/10 ="wu10.jpg"
/11 = "wu11.jpg"
/12 = "wu12.jpg"
/13 = "wu13.jpg"
/14 = "wu14.jpg"
/15 = "wu15.jpg"
/16 = "wu16.jpg"
/17 = "wu17.jpg"
/18 = "wu18.jpg"
/19 = "wu19.jpg"
/20 = "wu20.jpg"
</item>

<item objects_practice>
/1 = "wu01y.jpg"
/2 = "wu02y.jpg"
/3 = "wu03y.jpg"
/4 = "wu04y.jpg"
/5 = "wu05y.jpg"
/6 = "wu06y.jpg"
/7 = "wu07y.jpg"
/8 = "wu08y.jpg"
/9 = "wu09y.jpg"
/10 ="wu10y.jpg"
/11 = "wu11y.jpg"
/12 = "wu12y.jpg"
/13 = "wu13y.jpg"
/14 = "wu14y.jpg"
/15 = "wu15y.jpg"
/16 = "wu16y.jpg"
/17 = "wu17y.jpg"
/18 = "wu18y.jpg"
/19 = "wu19y.jpg"
/20 = "wu20y.jpg"
</item>

**********************************************
Practice Stimuli
**********************************************
<picture background_practice>
/ items = backgrounds_practice
/select =  values.selectbackground
/ position = (50%, 50%)
/ size = (80%, 80%)
/erase = false
</picture>

<picture object>
/ items = objects_practice
/select = values.itemnumber
/ position = (50%, 50%)
/ size = (80%, 80%)
</picture>

**************************************************************************************************************
**************************************************************************************************************
													   EXPERIMENTAL STIMULI
**************************************************************************************************************
**************************************************************************************************************
<item backgrounds>
/1 = "bg01.jpg"
/2 = "bg02.jpg"
/3 = "bg03.jpg"
/4 = "bg04.jpg"
/5 = "bg05.jpg"
/6 = "bg06.jpg"
/7 = "bg07.jpg"
/8 = "bg08.jpg"
/9 = "bg09.jpg"
/10 ="bg10.jpg"
/11 = "bg11.jpg"
/12 = "bg12.jpg"
/13 = "bg13.jpg"
/14 = "bg14.jpg"
/15 = "bg15.jpg"
/16 = "bg16.jpg"
/17 = "bg17.jpg"
/18 = "bg18.jpg"
/19 = "bg19.jpg"
/20 = "bg20.jpg"
</item>

<item GunWhites>
/1 = "bg01b.jpg"
/2 = "bg02b.jpg"
/3 = "bg03b.jpg"
/4 = "bg04b.jpg"
/5 = "bg05b.jpg"
/6 = "bg06b.jpg"
/7 = "bg07b.jpg"
/8 = "bg08b.jpg"
/9 = "bg09b.jpg"
/10 ="bg10b.jpg"
/11 = "bg11b.jpg"
/12 = "bg12b.jpg"
/13 = "bg13b.jpg"
/14 = "bg14b.jpg"
/15 = "bg15b.jpg"
/16 = "bg16b.jpg"
/17 = "bg17b.jpg"
/18 = "bg18b.jpg"
/19 = "bg19b.jpg"
/20 = "bg20b.jpg"
</item>

<item GunBlacks>
/1 = "bg01d.jpg"
/2 = "bg02d.jpg"
/3 = "bg03d.jpg"
/4 = "bg04d.jpg"
/5 = "bg05d.jpg"
/6 = "bg06d.jpg"
/7 = "bg07d.jpg"
/8 = "bg08d.jpg"
/9 = "bg09d.jpg"
/10 ="bg10d.jpg"
/11 = "bg11d.jpg"
/12 = "bg12d.jpg"
/13 = "bg13d.jpg"
/14 = "bg14d.jpg"
/15 = "bg15d.jpg"
/16 = "bg16d.jpg"
/17 = "bg17d.jpg"
/18 = "bg18d.jpg"
/19 = "bg19d.jpg"
/20 = "bg20d.jpg"
</item>

<item ObjectWhites>
/1 = "bg01a.jpg"
/2 = "bg02a.jpg"
/3 = "bg03a.jpg"
/4 = "bg04a.jpg"
/5 = "bg05a.jpg"
/6 = "bg06a.jpg"
/7 = "bg07a.jpg"
/8 = "bg08a.jpg"
/9 = "bg09a.jpg"
/10 ="bg10a.jpg"
/11 = "bg11a.jpg"
/12 = "bg12a.jpg"
/13 = "bg13a.jpg"
/14 = "bg14a.jpg"
/15 = "bg15a.jpg"
/16 = "bg16a.jpg"
/17 = "bg17a.jpg"
/18 = "bg18a.jpg"
/19 = "bg19a.jpg"
/20 = "bg20a.jpg"
</item>

<item ObjectBlacks>
/1 = "bg01c.jpg"
/2 = "bg02c.jpg"
/3 = "bg03c.jpg"
/4 = "bg04c.jpg"
/5 = "bg05c.jpg"
/6 = "bg06c.jpg"
/7 = "bg07c.jpg"
/8 = "bg08c.jpg"
/9 = "bg09c.jpg"
/10 ="bg10c.jpg"
/11 = "bg11c.jpg"
/12 = "bg12c.jpg"
/13 = "bg13c.jpg"
/14 = "bg14c.jpg"
/15 = "bg15c.jpg"
/16 = "bg16c.jpg"
/17 = "bg17c.jpg"
/18 = "bg18c.jpg"
/19 = "bg19c.jpg"
/20 = "bg20c.jpg"
</item>

**********************************************
Main Stimuli
**********************************************

<picture background>
/ items = backgrounds
/select = values.selectbackground
/ position = (50%, 50%)
/ size = (80%, 80%)
/erase = false
</picture>

<picture GunWhite>
/ items = GunWhites
/select = values.itemnumber
/ position = (50%, 50%)
/ size = (80%, 80%)
</picture>

<picture GunBlack>
/ items = GunBlacks
/select = values.itemnumber
/ position = (50%, 50%)
/ size = (80%, 80%)
</picture>

<picture ObjectWhite>
/ items = ObjectWhites
/select = values.itemnumber
/ position = (50%, 50%)
/ size = (80%, 80%)
</picture>

<picture ObjectBlack>
/ items = ObjectBlacks
/select = values.itemnumber
/ position = (50%, 50%)
/ size = (80%, 80%)
</picture>

**********************************************
Fixation Cross
**********************************************
<text fixation>
/ items = ("+")
/ position = (50%, 50%)
/ fontstyle = ("Arial", 5%, false, false, false, false, 5, 0)
/ txcolor = (black)
</text>

**********************************************
Feedback Stimuli
**********************************************
<text totalreport>
/ items = ("Punktestand: <%values.total%> Punkte")
/ fontstyle = ("Arial", 3%, false, false, false, false, 5, 0)
/ position = (50%, 50%)
/ txcolor = (black)
</text>

<item feedback>
/1 = "Guter Schuss!"
/2 = "Gute Entscheidung!"
/3 = "Sie sind tot!!!"
/4 = "Sie haben einen der Guten erschossen!"
/5 = "Zu langsam"
</item>

<text feedback>
/ items = feedback
/ fontstyle = ("Arial", 5%, false, false, false, false, 5, 0)
/ position = (50%, 20%)
/ txcolor = (black)
/ select = values.selectfeedback
</text>

<item result>
/1 = "Sie gewinnen <%values.HitReward%> Punkte "
/2 = "Sie gewinnen <%values.CRReward%> Punkte "
/3 = "Sie verlieren <%values.MissPunish%> Punkte "
/4 = "Sie verlieren <%values.FAPunish%> Punkte "
/5 = "Sie verlieren <%values.NoresponsePunish%> Punkte "
</item>

<text result>
/items = result
/ fontstyle = ("Arial", 5%, false, false, false, false, 5, 0)
/ position = (50%, 30%)
/ txcolor = (black)
/select = values.selectfeedback
</text>
**************************************************************************************************************
**************************************************************************************************************
													LISTS
**************************************************************************************************************
**************************************************************************************************************

*********************************
list.condition selects next experimental condition randomly without replacement
=> exactly 20 trials per experimental condition 
*********************************

<list condition>
/ items = (
1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4,
1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4, 1, 2, 3, 4)
/ replace = false
</list>

***********************************
the following four lists select the item number for each condition
without replacement => all items will be used
***********************************
<list itemnumber1>
/items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20)
/ replace = false
</list>

<list itemnumber2>
/items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20)
/ replace = false
</list>

<list itemnumber3>
/items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20)
/ replace = false
</list>

<list itemnumber4>
/items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20)
/ replace = false
</list>

***********************************
list.repetition selects randomly how many background pictures are presented
***********************************
<list repetition>
/ items = (1, 2, 3, 4)
/ replace = true
</list>

***********************************
list.duration selects randomly the duration of a background picture
***********************************
<list duration>
/ items = (500, 600, 700, 800, 900, 1000)
/replace = true
</list>

***********************************
the list selects the current 
background itemnumbers (unless it's the last background presented)

Backgrounds are selected randomly with replacement by default
***********************************
<list selectbackground>
/items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20)
/replace = true
</list>

**************************************************************************************************************
**************************************************************************************************************
											EXPERIMENTAL TRIALS
**************************************************************************************************************
**************************************************************************************************************
Trial Combo:
trial.fixation -> trial.background (loop) -> trial.maintrials (4 of them) -> trial.feedback

***********************************************************
trial.fixation
* presents fixation mark in the middle of the scree
*resets important variables
* determines randomly which experimental condition to present
* determines the itemnumber of the target image depending on condition
*determines the number of repititions of the background picture loop
*calls the first of trial.background
***********************************************************

<trial fixation>
/ ontrialbegin = [values.count_background = 0; values.Hit = 0; values.Miss = 0; values.FA = 0; values.CR = 0]
/ ontrialbegin = [values.condition = list.condition.nextvalue]
/ ontrialbegin = [if (values.condition == 1) values.itemnumber = list.itemnumber1.nextvalue]
/ ontrialbegin = [if (values.condition == 2) values.itemnumber = list.itemnumber2.nextvalue]
/ ontrialbegin = [if (values.condition == 3) values.itemnumber = list.itemnumber3.nextvalue]
/ ontrialbegin = [if (values.condition == 4) values.itemnumber = list.itemnumber4.nextvalue]
/ ontrialbegin = [values.repetition = list.repetition.nextvalue]

/ stimulusframes = [1 = fixation]
/ trialduration = values.fixationduration
/ validresponse = (noresponse)
/ branch = [trial.background]
/ recorddata = false
</trial>

***********************************************************
trial.background
* presents a background picture
=> if it is not the final background: it selects any background but the one
selected as the target background (no repeating backgrounds)
=> if it is the final background it selects the target background
* either calls itself for another round or calls the target
experimental condition
*resets list.selectbackground at the end of the loop
***********************************************************
<trial background>
/ ontrialbegin = [values.backgroundduration = list.duration.nextvalue]
/ ontrialbegin = [values.count_background += 1]
/ ontrialbegin = [if (values.count_background  < values.repetition) 
							{ values.selectbackground = list.selectbackground.nextvalue}
					else
							values.selectbackground = values.itemnumber]
/ stimulusframes = [1 = background]
/timeout = (values.backgroundduration)

/ branch = [if (values.count_background  < values.repetition) trial.background]
/ branch = [if (values.count_background  >= values.repetition && values.condition == 1)
					{reset(list.selectbackground); trial.GunWhite}]
/ branch = [if (values.count_background  >= values.repetition && values.condition == 2)
					{reset(list.selectbackground); trial.GunBlack}]
/ branch = [if (values.count_background  >= values.repetition && values.condition == 3)
					{reset(list.selectbackground); trial.ObjectWhite}]
/ branch = [if (values.count_background  >= values.repetition && values.condition == 4)
					{reset(list.selectbackground); trial.ObjectBlack}]
/ recorddata = false
</trial>

***************************************************************
Main Trials: 4 experimental conditions

*show target picture
*calculate total based on Hit (+10), Miss (-40), FA (-20), CR (+5)
* times out after 850ms (default) if no response made (-10)
*calls feedback
**************************************************************
<trial GunWhite>
/ ontrialbegin = [values.count_GunWhite += 1]
/ stimulusframes = [1 = GunWhite]

/ isvalidresponse = [trial.GunWhite.response == values.shootkey ||
							trial.GunWhite.response == values.noshootkey]
/ iscorrectresponse = [trial.GunWhite.response == values.shootkey]
/ timeout = (values.timeout)
/ ontrialend = [if (trial.GunWhite.response == values.shootkey) 
					{values.Hit = 1; values.total += values.HitReward;
					values.result = "Hit";
					values.Sum_WhiteHits += 1;
					values.SumRT_WhiteHits += trial.GunWhite.latency}]
/ ontrialend = [if (trial.GunWhite.response == values.noshootkey) 
					{values.Miss = 1; values.total -= values.MissPunish;
					values.result = "Miss";
					values.Sum_WhiteMisses += 1;
					values.SumRT_WhiteMisses += trial.GunWhite.latency }]
/ ontrialend = [if (trial.GunWhite.response == 0) 
					{values.total -= values.NoresponsePunish; values.result = "Noresponse"}]
/ branch = [trial.feedback]
</trial>


<trial GunBlack>
/ ontrialbegin = [values.count_GunBlack += 1]
/ stimulusframes = [1 = GunBlack]
/ isvalidresponse = [trial.GunBlack.response == values.shootkey || 
							trial.GunBlack.response == values.noshootkey]

/ iscorrectresponse = [trial.GunBlack.response == values.shootkey]
/ timeout = (values.timeout)
/ ontrialend = [if (trial.GunBlack.response == values.shootkey ) 
					{values.Hit = 1; values.total += values.HitReward;
					values.result = "Hit";
					values.Sum_BlackHits += 1;
					values.SumRT_BlackHits += trial.GunBlack.latency}]
/ ontrialend = [if (trial.GunBlack.response == values.noshootkey ) 
					{values.Miss = 1; values.total -= values.MissPunish;
					values.result = "Miss";
					values.Sum_BlackMisses += 1;
					values.SumRT_BlackMisses += trial.GunBlack.latency }]
/ ontrialend = [if (trial.GunBlack.response == 0) 
					{values.total -= values.NoresponsePunish; values.result = "Noresponse"}]
/ branch = [trial.feedback]
</trial>

<trial ObjectWhite>
/ ontrialbegin = [values.count_ObjectWhite += 1]
/ stimulusframes = [1 = ObjectWhite]
/ isvalidresponse = [trial.ObjectWhite.response == values.shootkey ||
							trial.ObjectWhite.response == values.noshootkey]

/ iscorrectresponse = [trial.ObjectWhite.response == values.noshootkey]
/ timeout = (values.timeout)
/ ontrialend = [if (trial.ObjectWhite.response == values.shootkey ) 
					{values.FA = 1; values.total -= values.FAPunish;
					values.result = "FA";
					values.Sum_WhiteFAs += 1;
					values.SumRT_WhiteFAs += trial.ObjectWhite.latency}]
/ ontrialend = [if (trial.ObjectWhite.response == values.noshootkey ) 
					{values.CR = 1; values.total += values.CRReward;
					values.result = "CR";
					values.Sum_WhiteCRs += 1;
					values.SumRT_WhiteCRs += trial.ObjectWhite.latency }]
/ ontrialend = [if (trial.ObjectWhite.response == 0) 
					{values.total -= values.NoresponsePunish; values.result = "Noresponse"}]
/ branch = [trial.feedback]
</trial>

<trial ObjectBlack>
/ ontrialbegin = [values.count_ObjectBlack += 1]
/ stimulusframes = [1 = ObjectBlack]
/ isvalidresponse = [trial.ObjectBlack.response == values.shootkey || 
							trial.ObjectBlack.response == values.noshootkey]

/ iscorrectresponse = [trial.ObjectBlack.response == values.noshootkey]
/ timeout = (values.timeout)
/ ontrialend = [if (trial.ObjectBlack.response == values.shootkey ) 
					{values.FA = 1; values.total -= values.FAPunish;
					values.result = "FA";
					values.Sum_BlackFAs += 1;
					values.SumRT_BlackFAs += trial.ObjectBlack.latency}]
/ ontrialend = [if (trial.ObjectBlack.response == values.noshootkey ) 
					{values.CR = 1; values.total += values.CRReward;
					values.result = "CR";
					values.Sum_BlackCRs += 1;
					values.SumRT_BlackCRs += trial.ObjectBlack.latency }]
/ ontrialend = [if (trial.ObjectBlack.response == 0) 
					{values.total -= values.NoresponsePunish; values.result = "Noresponse"}]
/ branch = [trial.feedback]
</trial>

*****************************************
feedback trial:
* presents accuracy feedback 
* explains how much was won/lost
*displays the total
****************************************
<trial feedback>
/ ontrialbegin = [if (values.Hit == 1) {text.feedback.textcolor = green; values.selectfeedback = 1}]
/ ontrialbegin = [if (values.CR == 1) {text.feedback.textcolor = green;values.selectfeedback = 2}]
/ ontrialbegin = [if (values.Miss == 1) {text.feedback.textcolor = red; values.selectfeedback = 3}]
/ ontrialbegin = [if (values.FA == 1) {text.feedback.textcolor = red; values.selectfeedback = 4}]
/ ontrialbegin = [if (values.Miss != 1 && values.Hit != 1 && values.CR != 1 && values.FA !=1)
						{text.feedback.textcolor = blue; values.selectfeedback = 5}]

/ stimulusframes = [1 = feedback, result, totalreport]
/ recorddata = false
/ timeout = (values.feedbackduration)
</trial>
**************************************************************************************************************
**************************************************************************************************************
											PRACTICE  TRIALS
**************************************************************************************************************
**************************************************************************************************************
Note:
*calls the 20 practice backgrounds in sequence
<trial fixation_practice>
/ ontrialbegin = [values.count_background = 0; values.Hit = 0; values.Miss = 0; values.FA = 0; values.CR = 0]
/ ontrialbegin = [values.itemnumber += 1]
/ ontrialbegin = [values.repetition = list.repetition.nextvalue]
/ stimulusframes = [1 = fixation]
/ trialduration = values.fixationduration
/ validresponse = (noresponse)
/ branch = [trial.background_practice]
/ recorddata = false
</trial>


<trial background_practice>
/ ontrialbegin = [values.backgroundduration = list.duration.nextvalue]
/ ontrialbegin = [values.count_background += 1]
/ ontrialbegin = [if (values.count_background  < values.repetition) 
							{
							values.selectbackground = list.selectbackground.nextvalue}
					else
							values.selectbackground = values.itemnumber]
/ stimulusframes = [1 = background_practice]
/timeout = (values.backgroundduration)
/ branch = [if (values.count_background  < values.repetition) trial.background_practice else trial.practiceobject]
</trial>

<trial practiceobject>
/ontrialbegin = [reset(list.selectbackground)]
/ stimulusframes = [1 = object]

/ isvalidresponse = [trial.practiceobject.response == values.shootkey ||
							trial.practiceobject.response == values.noshootkey]
/ timeout = (values.timeout)
/ iscorrectresponse = [
									((values.itemnumber == 4 || values.itemnumber == 7  || values.itemnumber == 8 ||
									values.itemnumber == 11 ||  values.itemnumber == 13 ||  values.itemnumber == 16 || 
									values.itemnumber == 17 ||  values.itemnumber == 18 ||  values.itemnumber == 19 || 
									values.itemnumber == 20)  && trial.practiceobject.response == values.shootkey) ||
									((values.itemnumber != 4 &&  values.itemnumber !=7  && values.itemnumber !=8 &&
									values.itemnumber != 11 &&  values.itemnumber != 13 &&  values.itemnumber != 16 &&
									values.itemnumber!=17 &&  values.itemnumber != 18 &&  values.itemnumber != 19 &&
									values.itemnumber != 20)  && trial.practiceobject.response == values.noshootkey) ]
/ ontrialend = [if ((values.itemnumber == 4 || values.itemnumber == 7  || values.itemnumber == 8 ||
									values.itemnumber == 11 ||  values.itemnumber == 13 ||  values.itemnumber == 16 || 
									values.itemnumber == 17 ||  values.itemnumber == 18 ||  values.itemnumber == 19 || 
									values.itemnumber == 20)  && trial.practiceobject.response == values.shootkey) 
									{values.Hit = 1; values.total += values.HitReward}]
/ ontrialend = [if ((values.itemnumber == 4 || values.itemnumber == 7  || values.itemnumber == 8 ||
									values.itemnumber == 11 ||  values.itemnumber == 13 ||  values.itemnumber == 16 || 
									values.itemnumber == 17 ||  values.itemnumber == 18 ||  values.itemnumber == 19 || 
									values.itemnumber == 20)  && trial.practiceobject.response == values.noshootkey) 
									{values.Miss = 1; values.total -= values.MissPunish}]
/ ontrialend = [if ((values.itemnumber != 4 &&  values.itemnumber !=7  && values.itemnumber !=8 &&
									values.itemnumber != 11 &&  values.itemnumber != 13 &&  values.itemnumber != 16 &&
									values.itemnumber!=17 &&  values.itemnumber != 18 &&  values.itemnumber != 19 &&
									values.itemnumber != 20)  && trial.practiceobject.response == values.noshootkey) 
									{values.CR = 1; values.total += values.CRReward}]
/ ontrialend = [if ((values.itemnumber != 4 &&  values.itemnumber !=7  && values.itemnumber !=8 &&
									values.itemnumber != 11 &&  values.itemnumber != 13 &&  values.itemnumber != 16 &&
									values.itemnumber!=17 &&  values.itemnumber != 18 &&  values.itemnumber != 19 &&
									values.itemnumber != 20)  && trial.practiceobject.response == values.shootkey) 
									{values.FA = 1; values.total = values.total - values.FAPunish}]
/ ontrialend = [if (trial.practiceobject.response == 0) values.total -= values.NoresponsePunish]
/ branch = [trial.feedback]
</trial>



**************************************************************************************************************
**************************************************************************************************************
											EXPERIMENTAL BLOCKS
**************************************************************************************************************
**************************************************************************************************************
Note: 
*runs 80 of the trial-combos
* counterbalanced for shootkey/noshootkey  (see EXPERIMENT)

<block POD_A>
/ preinstructions = (taskA)
/ onblockbegin = [values.total = 0]
/ trials = [1-80 = fixation]
/ postinstructions = (end, results)
</block>

<block POD_B>
/ preinstructions = (taskB)
/ onblockbegin = [values.total = 0]
/ trials = [1-80 = fixation]
/ postinstructions = (end, results)
</block>
**************************************************************************************************************
**************************************************************************************************************
											PRACTICE BLOCKS
**************************************************************************************************************
**************************************************************************************************************
Note: 
*runs 20 of the practice trial-combos
* counterbalanced for shootkey/noshootkey  (see EXPERIMENT)
* data recorded, set to /recorddata = false to change

<block POD_practiceA>
/ preinstructions = (practiceA)
/ onblockbegin = [values.total = 0]
/ trials = [1-20 = fixation_practice]
/ recorddata = true
</block>

<block POD_practiceB>
/ preinstructions = (practiceB)
/ onblockbegin = [values.total = 0]
/ trials = [1-20 = fixation_practice]
/ recorddata = true
</block>


**********************************************************************************************
HYPOTHESEN
**********************************************************************************************
<textbox H1>
/ caption = "Bitte beschreiben Sie kurz, worum es Ihrer Meinung nach in dieser Untersuchung ging:"
/ fontstyle = ("Verdana", 24, false, false, false, false, 5, 0)
/ multiline = true
/ textboxsize=(50,50)
</textbox>

<surveypage H1>
/ caption = ""
/ fontstyle = ("Verdana", -20, true, false, false, false, 5, 0)
/ questions = [1=H1]
/ nextlabel = "Weiter"
/finishlabel ="Weiter"
/ navigationbuttonsize = (250px, 50px)
/ showquestionnumbers = false
</surveypage>

<surveypage H2>
/ showpagenumbers = false
/ caption = "In der vorherigen Aufgabe ging es darum zu untersuchen wie Menschen unter Zeitdruck entscheiden. Hierbei ist es jedoch denkbar, dass bestimmte Eigenschaften der Personen beeinflussen, ob wir ihnen eher ausweichen oder auf Sie zugehen.

Um hinsichtlich dieser Einflussgröße kontrollieren zu können, bitten wir Sie im Folgenden einige Fragen zu Personen und sozialen Gruppen in dieser Gesellschaft zu beantworten. Bitte beachten Sie, dass es hierbei keine richtigen oder falschen Antworten gibt. Es ist wichtig, dass Sie jede der Fragen offen und ehrlich beantworten.
"
/ nextlabel = "Weiter"
 /finishlabel ="Weiter"
/ fontstyle = ("Verdana", 24, false, false, false, false, 5, 0)
</surveypage>

<survey VP_Hypothesen>
/ pages = [1=H1; 2 = H2]
/ responsefontstyle = ("Verdana", -20, false, false, false, false, 5, 0)
/ itemfontstyle = ("Verdana", -13, false, false, false, false, 5, 0)
/ itemspacing = 10%
/ showpagenumbers = false
</survey>


**********************************************************************************************
FEELING THERMOMETER
**********************************************************************************************

<surveypage intro1>
/ showpagenumbers = false
/ caption = "Wir bitten Sie im Folgenden verschiedene soziale Gruppen in unserer Gesellschaft auf einer Thermometer-Skala danach zu beurteilen, wie warm bzw. kalt Sie sich ihnen gegenüber fühlen."
/ fontstyle = ("Verdana", 24, false, false, false, false, 5, 0)
</surveypage>

<text warm>
/ fontstyle = ("Verdana", 20, false, false, false, false, 5, 0)
/ items = ("sehr warm")
/ txcolor = (255, 0, 0)
/ txbgcolor = (255, 255, 255)
/ position = (10, 24)
/ erase = false
</text>

<text cold>
/ fontstyle = ("Verdana", 20, false, false, false, false, 5, 0)
/ items = ("sehr kalt")
/ txcolor = (0, 0, 255)
/ txbgcolor = (255, 255, 255)
/ position = (10, 73)
/ erase = false
</text>

<picture arrow>
/ items = ("arrow.png")
/ position = (10, 49)
/ size = (80, 488)
/ erase = false
</picture>

<surveypage thermometer>
/ showpagenumbers = false
/ stimulusframes = [1=warm, cold, arrow]
/ caption = "Im Folgenden sehen Sie verschiedene soziale Gruppen. Wir bitten Sie diese Gruppen auf Thermometer-Skalen danach zu beurteilen, wie warm bzw. kalt Sie sich ihnen gegenüber fühlen. Für Ihre Beurteilung können Sie die Schieberegler nach oben (warm) und nach unten (kalt) ziehen."
/ fontstyle = ("Verdana", 24, false, false, false, false, 5, 0)
/ questions = [1=Buddhisten; 2=Christen; 3=Atheisten; 4=Muslime; 5=Russen; 6=Türken; 7=Deutsche; 8 = Italiener] 
</surveypage>

<slider Buddhisten>
/ increment = 1
/ orientation = vertical
/ position = (15, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Buddhisten"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Christen>
/ increment = 1
/ orientation = vertical
/ position = (25, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Christen"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Atheisten>
/ increment = 1
/ orientation = vertical
/ position = (35, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Atheisten"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Muslime>
/ increment = 1
/ orientation = vertical
/ position = (45, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Muslime"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Russen>
/ increment = 1
/ orientation = vertical
/ position = (55, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Russen"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Türken>
/ increment = 1
/ orientation = vertical
/ position = (65, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Türken"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Deutsche>
/ increment = 1
/ orientation = vertical
/ position = (75, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Deutsche"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<slider Italiener>
/ increment = 1
/ orientation = vertical
/ position = (85, 20)
/ showticks = true
/ showtooltips = false
/ labels = ("+50", "0", "-50")
/ caption = "Italiener"
/ fontstyle = ("Verdana", 13pt, false, false, false, false, 5, 0)
/ slidersize = (2, 50)
/ range = (0, 100)
</slider>

<survey feeling>
/ pages = [1=intro1; 2 = thermometer; 3 = traitintro]
/ responsefontstyle = ("Verdana", -13, false, false, false)
/ itemfontstyle = ("Verdana", -16, true, false, false)
/ itemspacing = 4%
/ showpagenumbers = false
/ finishlabel = "weiter per Mausclick"
/ nextlabel = "weiter per Mausclick"
</survey>



**********************************************************************************************
TRAIT RATINGS
**********************************************************************************************

* wird im survey feeling aufgerufen

<surveypage traitintro>
/ showpagenumbers = false
/ caption = "So wie wir Menschen häufig mit bestimmten Eigenschaften beschreiben, werden auch sozialen Gruppen in der Gesellschaft auf unterschiedliche Art beschrieben. Im Folgenden sind wir an Ihrer persönlichen Einschätzung interessiert. Bitte geben Sie an, wie sehr Ihrer Meinung nach bestimmte Eigenschaften auf verschiedene soziale Gruppen in der Gesellschaft zutreffen.

Bitte beachten Sie, dass es hierbei keine richtigen oder falschen Antworten gibt. Es ist wichtig, dass Sie jede der Fragen offen und ehrlich beantworten."
/ fontstyle = ("Verdana", 24, false, false, false, false, 5, 0)
</surveypage>

<page muslim_intro>Bitte geben Sie nun an, auf wieviel Prozent aller MUSLIMISCHEN MÄNNER in Deutschland die folgenden Eigenschaften zutreffen.
</page>

<text bglab_in>
/ numitems = 1
/ items = ("Auf wieviel Prozent aller DEUTSCHEN trifft die folgende Eigenschaft zu?")
/ fontstyle = ("Verdana", 20pt, true, false, false, false, 5, 0)
/ position = (50, 40)
</text>

<text bglab_out>
/ numitems = 1
/ items = ("Auf wieviel Prozent aller MUSLIMISCHEN MÄNNER in Deutschland trifft die folgende Eigenschaft zu?")
/ fontstyle = ("Verdana", 20pt, true, false, false, false, 5, 0)
/ position = (50, 40)
</text>

<text threat>
/ numitems = 18
/ items = ("gefährlich", "kriminell", "bedrohlich", "Täter", "terroristisch", 
			"gewalttätig", "aggressiv", "radikal", "fanatisch", "ungefährlich", 
			"gesetzestreu", "harmlos", "Opfer", "friedfertig", "friedlich", 
			"ausgeglichen", "gemäßigt", "besonnen")
/ select = noreplacenorepeat
/ resetinterval = 2
/ fontstyle = ("Verdana", 30pt, true, false, false, false, 5, 0)
/ position = (50,60)
</text>

<text other>
/ numitems = 20
/ items = ("schmarotzend", "barbarisch", "irrational", "primitiv", "sexistisch", 
			"fremdartig", "undemokratisch", "traditionell", "ungebildet", "intolerant", 
			"beitragend", "zivilisiert", "rational", "kultiviert", "geschlechtergerecht", 
			"vertraut", "demokratisch", "fortschrittlich", "gebildet", "tolerant")
/ select = noreplacenorepeat
/ resetinterval = 2
/ fontstyle = ("Verdana", 30pt, true, false, false, false, 5, 0)
/ position = (50,60)
</text>

<likert rating>
/ stimulusframes = [1 = noreplacenorepeat(threat, other)]
/ numpoints = 11
/ anchorwidth=100
/ buttonvalues = [1 = "0", 2 = "10", 3 = "20", 4= "30", 5 = "40", 6 = "50", 7 = "60", 8 = "70", 9 = "80", 10 = "90", 11 = "100"]
/ fontstyle = ("Verdana", 16pt, false, false, false, false, 5, 0)
/ position = (50,80)
/ mouse = true
/ posttrialpause = 500
</likert>

<block traits_in>
/ bgstim = (bglab_in)
/ trials = [1-38 = noreplacenorepeat(rating)]
</block>

<block traits_out>
/ preinstructions = (muslim_intro)
/ bgstim = (bglab_out)
/ trials = [1-38 = noreplacenorepeat(rating)]
</block>


**********************************************************************************************
Kontakt
**********************************************************************************************

<page contactintro>Im Folgenden sind wir interessiert zu erfahren, inwiefern Sie in verschiedenen Lebensbereichen mit muslimischen Menschen Kontakt haben oder hatten. Bitte geben Sie dafür an, inwiefern verschiedene Arten von Beziehungen mit muslimischen Menschen auf Sie zutreffen.
</page>

<text c_items>
/ numitems = 8
/ items = ("Ich kenne viele muslimische Menschen.",
			"Ich lebe oder habe in einer Gegend gelebt, in der hauptsächlich muslimische Menschen leben.",
			"In meiner Freizeit habe ich Kontakt zu muslimischen Menschen.", 
			"In meiner Schule hatte ich Kontakt mit muslimischen Mitschüler*innen.", 
			"Ich unterhalte mich häufig mit muslimischen Menschen.", 
			"Ich habe generell nur Kontakt zu muslimischen Menschen.", 
			"In meinem Alltag habe ich Kontakt zu muslimischen Menschen.")
/ select = noreplacenorepeat
/ resetinterval = 2
/ fontstyle = ("Verdana", 18pt, true, false, false, false, 5, 0)
/ position = (50,50)
</text>

<likert contact>
/ stimulusframes = [1 = noreplacenorepeat(c_items)]
/ numpoints = 6
/ anchorwidth=200
/ anchors =  [1 = "stimme gar nicht zu"; 2 = "stimme nicht zu"; 3 = "stimme eher nicht zu"; 4 = "stimme eher zu"; 5 = "stimme zu" ; 6 = "stimme voll zu"]
/ buttonvalues = [1 = "1", 2 = "2", 3 = "3", 4= "4", 5 = "5", 6 = "6"]
/ position = (50,80)
/ mouse = true
/ posttrialpause = 500
</likert>

<block contact>
/ preinstructions = (contactintro)
/ trials = [1-8 = noreplacenorepeat(contact)]
</block>


******************************************************************************************
					Stereotypikalitätsrating
******************************************************************************************
<picture st_rating>
/ items = (
"01a.jpg"
"01c.jpg"
"01d.jpg"
"02a.jpg"
"02b.jpg"
"02c.jpg"
"02d.jpg"
"03a.jpg"
"03b.jpg"
"03c.jpg"
"03d.jpg"
"04a.jpg"
"04b.jpg"
"04c.jpg"
"04d.jpg"
"05a.jpg"
"05b.jpg"
"05c.jpg"
"05d.jpg"
"06a.jpg"
"06b.jpg"
"06c.jpg"
"06d.jpg"
"07a.jpg"
"07b.jpg"
"07c.jpg"
"07d.jpg"
"08a.jpg"
"08b.jpg"
"08c.jpg"
"08d.jpg"
"09a.jpg"
"09b.jpg"
"09c.jpg"
"09d.jpg"
"10a.jpg"
"10b.jpg"
"10c.jpg"
"10d.jpg"
"11a.jpg"
"11b.jpg"
"11c.jpg"
"11d.jpg"
"12a.jpg"
"12b.jpg"
"12c.jpg"
"12d.jpg"
"13a.jpg"
"13b.jpg"
"13c.jpg"
"13d.jpg"
"14a.jpg"
"14b.jpg"
"14c.jpg"
"14d.jpg"
"15a.jpg"
"15b.jpg"
"15c.jpg"
"15d.jpg"
"16a.jpg"
"16b.jpg"
"16c.jpg"
"16d.jpg"
"17a.jpg"
"17b.jpg"
"17c.jpg"
"17d.jpg"
"18a.jpg"
"18b.jpg"
"18c.jpg"
"18d.jpg"
"19a.jpg"
"19b.jpg"
"19c.jpg"
"19d.jpg"
"20a.jpg"
"20b.jpg"
"20c.jpg"
"20d.jpg")
/ select = noreplacenorepeat
/ resetinterval = 1
/ size = (20%, 20%)
</picture>

<likert typicality>
/ stimulusframes = [1 = noreplacenorepeat(st_rating)]
/ numpoints = 7
/ anchorwidth=100
/ anchors = [1 = "überhaupt nicht 
muslimisch"; 7 = "sehr 
muslimisch"]
/ buttonvalues = [1 = "1", 2 = "2", 3= "3", 4 = "4", 5 = "5", 6 = "6", 7 = "7"]
/ fontstyle = ("Verdana", 18pt, false, false, false, false, 5, 0)
/ position = (50,85)
/ mouse = true
/ posttrialpause = 750
</likert>

<text bg_stereo>
/ numitems = 1
/ items = ("Wie muslimisch wirkt die gezeigte Person?")
/ fontstyle = ("Verdana", 18pt, true, false, false, false, 5, 0)
/ position = (50, 20)
</text>

<block stereo>
/preinstructions = (stereo_intro)
/ bgstim = (bg_stereo)
/ trials = [1-80 = noreplacenorepeat(typicality)]
</block>

<page stereo_intro>Im Folgenden sehen Sie die Personen aus der Computerspiel-Aufgabe noch einmal. 
Bitte beurteilen Sie die Personen nun auf einer Skala von 1 bis 7 danach, wie typisch muslimisch diese aussehen.
Niedrige Werte bedeuten, dass die gezeigte Person überhaupt nicht muslimisch auf Sie wirkt. 
Hohe Werte bedeuten, dass die gezeigte Person sehr muslimisch auf Sie wirkt.
</page>

**********************************************************************************************
DEMOGRAFISCHE DATEN
**********************************************************************************************

<survey demo>
/ pages = [1=demo1, demo2, demo3]
/ itemspacing = 4%
/ showpagenumbers = false
/ finishlabel = "weiter per Mausclick"
/ nextlabel = "weiter mit Mausclick"
</survey>

<surveypage demo1>
/ caption = "Bitte beantworten Sie zum Abschluss noch einige demographische Fragen:"
/ questions = [1=age; 2=sex; 3=hand; 4=education] 
</surveypage>

<surveypage demo2>
/ questions = [1=language; 2=ger1; 3=nat1; 4=nat2; 5=ethnicity] 
</surveypage>

<surveypage demo3>
/ questions = [1=experience; 2=comments] 
</surveypage>

<radiobuttons sex>
/ caption = "Was ist Ihr Geschlecht?"
/ options = ("weiblich", "männlich", "andere")
/ required = false
</radiobuttons>

<textbox age>
/ caption = "Wie alt sind Sie? Bitte tippen Sie die Zahl (in Jahren) ein."
/ mask = positiveinteger
/ range = (15, 99)
/ required = false
/ textboxsize = (4, 2)
</textbox>

<radiobuttons hand>
/ caption = "Sind Sie links- oder rechtshändig?"
/ options = ("linkshändig", "rechtshändig","beidhändig")
/ required = false
</radiobuttons>

<radiobuttons education>
/ caption = "Welchen höchsten allgemeinen Bildungsabschluss haben Sie?"
/ options = ("(Noch) keinen Bildungsabschluss", "Hauptschulabschluss (Volksschulabschluss)", "Realschulabschluss (Mittlere Reife)", "Fachhochschulreife, Abschluss einer Fachoberschule", "Allgemeine Hochschulreife/Abitur")
/ other = "Abgeschlossenes Studium, nämlich:"
/ required = false
</radiobuttons>


<radiobuttons language>
/ caption = "Ist Deutsch Ihre Muttersprache?"
/ options = ("Ja - einsprachig aufgewachsen", "Ja - mehrsprachig aufgewachsen" )
/ other = "Nein, meine Muttersprache ist:"
/ required = false
</radiobuttons>

<radiobuttons ger1>
/ caption = "Sind Sie in Deutschland geboren?"
/ options = ("Ja")
/ other = "Nein, in einem anderen Land, und zwar:"
/ required = false
</radiobuttons>

<radiobuttons nat1>
/ caption = "Haben Sie die deutsche Staatsangehörigkeit?"
/ options = ("Ja", "Nein" )
/ required = false
</radiobuttons>

<radiobuttons nat2>
/ caption = "Haben Sie die Staatsangehörigkeit eines anderen Landes?"
/ options = ("Nein")
/ other = "Ja, und zwar:"
/ required = false
</radiobuttons>

<radiobuttons ethnicity>
/ caption = "Wie würden Sie Ihren ethnisch-kulturellen Hintergrund bezeichnen?"
/ options = ("Deutsch")
/ other = "Anders, nämlich:"
/ required = false
</radiobuttons>

<textbox experience>
/ caption = "Kennen Sie diese oder eine ähnliche 'Schieß-Aufgabe' bereits aus ihrem Studium oder auch aus einem anderen Kontext? Wenn ja, dann beschreiben Sie diese kurz:"
/ multiline = false
/ maxchars = 1000
/ textboxsize = (80, 10)
/ required = false
</textbox>

<textbox comments>
/ caption = "Haben Sie noch weitere Anmerkungen zu dieser Studie?"
/ multiline = false
/ maxchars = 1000
/ textboxsize = (80, 10)
/ required = false
</textbox>


**********************************************************************************************
DEBRIEFING
**********************************************************************************************
<page Debrief>INFORMATIONEN ZU DIESER STUDIE
^^Das Ziel dieser Studie bestand darin, zu untersuchen wie gut Menschen auf die Schnelle erkennen können, ob eine Person bedrohlich ist (z.B. eine Waffe in der Hand hält), 
oder ob eine Person nicht bedrohlich ist (d.h., einen harmlosen Gegenstand in der Hand hält). Wir möchten erforschen, ob das Erkennen von Bedrohlichkeit durch die ethnische oder religiöse Zugehörigkeit
der Personen beeinflusst wird. Dabei ist es denkbar, dass die Situation häufiger (fälschlicherweise) als bedrohlich eingeschätzt wird, wenn die Person auf der Straße muslimisch oder arabisch aussieht.
^^Wenn Sie noch Fragen zu der Studie haben, wenden Sie sich bitte an die Versuchsleitung.
</page>


**************************************************************************************************************
**************************************************************************************************************
												 EXPERIMENT 
**************************************************************************************************************
**************************************************************************************************************
Experiment counterbalanced for shootkey/noshootkey (assignment by subjectnumber)

*odd subjectnumbers:
<expt >
/ subjects = (1 of 2)
/ onexptbegin = [values.shootkey = values.key1; values.noshootkey = values.key2; values.group = 1]
/ blocks = [1 = POD_practiceA; 2 = POD_A; 3 = VP_Hypothesen; 4 = feeling; 5 = traits_in; 6=traits_out; 7 = contact; 8 = stereo; 9 = demo]
/ preinstructions = (Intro, intro1_A, intro2)
/ postinstructions = (Debrief)
</expt>

*even subjectnumbers:
<expt >
/ subjects = (2 of 2)
/ onexptbegin = [values.shootkey = values.key2; values.noshootkey = values.key1; values.group = 2]
/ blocks = [1 = POD_practiceB; 2 = POD_B; 3 = VP_Hypothesen; 4 = feeling; 5 = traits_in; 6=traits_out; 7 = contact; 8 = stereo; 9 = demo]
/ preinstructions = (Intro, intro1_B, intro2)
/ postinstructions = (Debrief)
</expt>

**************************************************************************************************************
**************************************************************************************************************
													END OF FILE
**************************************************************************************************************
**************************************************************************************************************